/*****************************************************************************\
* (c) Copyright 2018-2020 CERN for the benefit of the LHCb Collaboration      *
*                                                                             *
* This software is distributed under the terms of the Apache License          *
* version 2 (Apache-2.0), copied verbatim in the file "LICENSE".              *
*                                                                             *
* In applying this licence, CERN does not waive the privileges and immunities *
* granted to it by virtue of its status as an Intergovernmental Organization  *
* or submit itself to any jurisdiction.                                       *
\*****************************************************************************/
#pragma once
#include "ParKalmanFittedTrack.cuh"
#include "ParKalmanMath.cuh"
#include "PV_Definitions.cuh"
#include "patPV_Definitions.cuh"
#include "CheckerTracks.cuh"
#include "ParticleTypes.cuh"

__device__ inline void prepare_downstream_tracks(
  const Allen::Views::Physics::DownstreamTracks downstream_tracks,
  const Allen::Views::Physics::KalmanStates downstream_states,
  Checker::Track* downstream_checker_tracks)
{
  const auto number_of_tracks = downstream_tracks.size();
  for (unsigned track_idx = threadIdx.x; track_idx < number_of_tracks; track_idx += blockDim.x) {

    Checker::Track t;
    const auto downstream_track = downstream_tracks.track(track_idx);
    const auto ut_segment = downstream_track.track_segment<Allen::Views::Physics::Track::segment::ut>();
    const auto downstream_state = downstream_states.state(ut_segment.track_index());

    t.qop = downstream_state.qop();
    t.p = downstream_state.p();
    t.pt = downstream_state.pt();
    t.rho = downstream_state.rho();

    const auto total_number_of_hits = downstream_track.number_of_hits();
    for (unsigned int ihit = 0; ihit < total_number_of_hits; ihit++) {
      const auto id = downstream_track.get_id(ihit);
      t.addId(id);
    }

    downstream_checker_tracks[track_idx] = t;
  };
}

__device__ inline void prepare_long_tracks(
  const Allen::Views::Physics::LongTracks event_long_tracks,
  const Allen::Views::Physics::KalmanStates endvelo_states,
  Checker::Track* long_checker_tracks)
{
  const unsigned number_of_tracks_event = event_long_tracks.size();
  for (unsigned i_track = threadIdx.x; i_track < number_of_tracks_event; i_track += blockDim.x) {
    Checker::Track t;
    const auto long_track = event_long_tracks.track(i_track);

    const auto velo_track = long_track.track_segment<Allen::Views::Physics::Track::segment::velo>();
    const auto velo_track_index = velo_track.track_index();
    const auto velo_state = endvelo_states.state(velo_track_index);
    t.velo_track_index = velo_track_index;
    // momentum
    const auto qop = long_track.qop();
    t.p = 1.f / std::abs(qop);
    t.qop = qop;
    // direction at first state -> velo state of track
    const float tx = velo_state.tx();
    const float ty = velo_state.ty();
    const float slope2 = tx * tx + ty * ty;
    t.pt = std::sqrt(slope2 / (1.0f + slope2)) / std::fabs(qop);
    // pseudorapidity
    const float rho = std::sqrt(slope2);
    t.rho = rho;
    t.eta = eta_from_rho(rho);
    t.phi = atan2f(ty, tx);

    // add all hits
    const auto total_number_of_hits = long_track.number_of_hits();
    for (unsigned int ihit = 0; ihit < total_number_of_hits; ihit++) {
      const auto id = long_track.get_id(ihit);
      // if (lhcb_id::is_ut(id))
      t.addId(id);
    }
    long_checker_tracks[i_track] = t;
  }
}

__device__ inline void
prepare_muons(const unsigned number_of_tracks_event, Checker::Track* long_checker_tracks, const bool* is_muon)
{
  for (unsigned i_track = threadIdx.x; i_track < number_of_tracks_event; i_track += blockDim.x) {
    long_checker_tracks[i_track].is_muon = is_muon[i_track];
  }
}

__device__ inline float ipKalman(const ParKalmanFilter::FittedTrack& track, const PV::Vertex& vertex)
{
  // Get position information.
  float tx = track.state[2];
  float ty = track.state[3];
  float dz = vertex.position.z - track.z;
  float dx = track.state[0] + dz * tx - vertex.position.x;
  float dy = track.state[1] + dz * ty - vertex.position.y;
  return std::sqrt((dx * dx + dy * dy) / (1.0f + tx * tx + ty * ty));
}

__device__ inline float ipxKalman(const ParKalmanFilter::FittedTrack& track, const PV::Vertex& vertex)
{
  // Get position information.
  float tx = track.state[2];
  float dz = vertex.position.z - track.z;
  float dx = track.state[0] + dz * tx - vertex.position.x;
  return dx;
}

__device__ inline float ipyKalman(const ParKalmanFilter::FittedTrack& track, const PV::Vertex& vertex)
{
  // Get position information.
  float ty = track.state[3];
  float dz = vertex.position.z - track.z;
  float dy = track.state[1] + dz * ty - vertex.position.y;
  return dy;
}

__device__ inline float ipChi2Kalman(const ParKalmanFilter::FittedTrack& track, const PV::Vertex& vertex)
{
  // Get position information.
  float tx = track.state[2];
  float ty = track.state[3];
  float dz = vertex.position.z - track.z;
  float dx = track.state[0] + dz * tx - vertex.position.x;
  float dy = track.state[1] + dz * ty - vertex.position.y;

  // Build covariance matrix.
  float cov00 = vertex.cov00 + track.cov(0, 0);
  float cov10 = vertex.cov10;
  float cov11 = vertex.cov11 + track.cov(1, 1);

  // Add contribution from extrapolation.
  cov00 += dz * dz * track.cov(2, 2) + 2 * std::abs(dz * track.cov(2, 0));
  cov11 += dz * dz * track.cov(3, 3) + 2 * dz * track.cov(3, 1);

  // Add the contribution from the PV z position.
  cov00 += tx * tx * vertex.cov22 - 2 * tx * vertex.cov20;
  cov10 += tx * ty * vertex.cov22 - ty * vertex.cov20 - tx * vertex.cov21;
  cov11 += ty * ty * vertex.cov22 - 2 * ty * vertex.cov21;

  // Invert the covariance matrix.
  float D = cov00 * cov11 - cov10 * cov10;
  float invcov00 = cov11 / D;
  float invcov10 = -cov10 / D;
  float invcov11 = cov00 / D;

  return dx * dx * invcov00 + 2 * dx * dy * invcov10 + dy * dy * invcov11;
}

__device__ inline float kalmanDOCAz(const ParKalmanFilter::FittedTrack& track, const PV::Vertex& vertex)
{
  float dx = track.state[0] - vertex.position.x;
  float dy = track.state[1] - vertex.position.y;
  float tx = track.state[2];
  float ty = track.state[3];
  return std::abs(ty * dx - tx * dy) / std::sqrt(tx * tx + ty * ty);
}

__device__ inline float ipVelo(const Allen::Views::Physics::KalmanState& velo_kalman_state, const PV::Vertex& vertex)
{
  // ORIGIN: Rec/Tr/TrackKernel/src/TrackVertexUtils.cpp
  float tx = velo_kalman_state.tx();
  float ty = velo_kalman_state.ty();
  float dz = vertex.position.z - velo_kalman_state.z();
  float dx = velo_kalman_state.x() + dz * tx - vertex.position.x;
  float dy = velo_kalman_state.y() + dz * ty - vertex.position.y;
  return std::sqrt((dx * dx + dy * dy) / (1.0f + tx * tx + ty * ty));
}

__device__ inline float ipxVelo(const Allen::Views::Physics::KalmanState& velo_kalman_state, const PV::Vertex& vertex)
{
  // ORIGIN: Rec/Tr/TrackKernel/src/TrackVertexUtils.cpp
  float tx = velo_kalman_state.tx();
  float dz = vertex.position.z - velo_kalman_state.z();
  float dx = velo_kalman_state.x() + dz * tx - vertex.position.x;
  return dx;
}

__device__ inline float ipyVelo(const Allen::Views::Physics::KalmanState& velo_kalman_state, const PV::Vertex& vertex)
{
  // ORIGIN: Rec/Tr/TrackKernel/src/TrackVertexUtils.cpp
  float ty = velo_kalman_state.ty();
  float dz = vertex.position.z - velo_kalman_state.z();
  float dy = velo_kalman_state.y() + dz * ty - vertex.position.y;
  return dy;
}

__device__ inline float ipChi2Velo(
  const Allen::Views::Physics::KalmanState& velo_kalman_state,
  const PV::Vertex& vertex)
{
  // ORIGIN: Rec/Tr/TrackKernel/src/TrackVertexUtils.cpp
  float tx = velo_kalman_state.tx();
  float ty = velo_kalman_state.ty();
  float dz = vertex.position.z - velo_kalman_state.z();
  float dx = velo_kalman_state.x() + dz * tx - vertex.position.x;
  float dy = velo_kalman_state.y() + dz * ty - vertex.position.y;

  // compute the covariance matrix. first only the trivial parts:
  float cov00 = vertex.cov00 + velo_kalman_state.c00();
  float cov10 = vertex.cov10; // state c10 is 0.f;
  float cov11 = vertex.cov11 + velo_kalman_state.c11();

  // add the contribution from the extrapolation
  cov00 += dz * dz * velo_kalman_state.c22() + 2 * std::abs(dz * velo_kalman_state.c20());
  // cov10 is unchanged: state c32, c30 and c21 are  0.f
  cov11 += dz * dz * velo_kalman_state.c33() + 2 * dz * velo_kalman_state.c31();

  // add the contribution from pv Z
  cov00 += tx * tx * vertex.cov22 - 2 * tx * vertex.cov20;
  cov10 += tx * ty * vertex.cov22 - ty * vertex.cov20 - tx * vertex.cov21;
  cov11 += ty * ty * vertex.cov22 - 2 * ty * vertex.cov21;

  // invert the covariance matrix
  float D = cov00 * cov11 - cov10 * cov10;
  float invcov00 = cov11 / D;
  float invcov10 = -cov10 / D;
  float invcov11 = cov00 / D;

  return dx * dx * invcov00 + 2 * dx * dy * invcov10 + dy * dy * invcov11;
}

__device__ inline float veloDOCAz(const Allen::Views::Physics::KalmanState& velo_kalman_state, const PV::Vertex& vertex)
{
  float dx = velo_kalman_state.x() - vertex.position.x;
  float dy = velo_kalman_state.y() - vertex.position.y;
  float tx = velo_kalman_state.tx();
  float ty = velo_kalman_state.ty();
  return std::abs(ty * dx - tx * dy) / std::sqrt(tx * tx + ty * ty);
}

__device__ inline void prepare_kalman_tracks(
  const unsigned number_of_tracks,
  const unsigned number_of_vertices,
  const PV::Vertex* rec_vertices,
  const Allen::Views::Physics::KalmanStates endvelo_states,
  const ParKalmanFilter::FittedTrack* kf_tracks,
  Checker::Track* kalman_checker_tracks)
{
  for (unsigned i_track = threadIdx.x; i_track < number_of_tracks; i_track += blockDim.x) {
    ParKalmanFilter::FittedTrack track = kf_tracks[i_track];
    auto t = kalman_checker_tracks[i_track];
    const auto velo_state = endvelo_states.state(t.velo_track_index);
    // Calculate IP.
    t.kalman_ip_chi2 = 9999.;
    t.velo_ip_chi2 = 9999.;
    for (unsigned i_vertex = 0; i_vertex < number_of_vertices; ++i_vertex) {
      const auto vertex = rec_vertices[i_vertex];

      float locIPChi2 = ipChi2Kalman(track, vertex);
      if (locIPChi2 < t.kalman_ip_chi2) {
        t.kalman_ip = ipKalman(track, vertex);
        t.kalman_ip_chi2 = locIPChi2;
        t.kalman_ipx = ipxKalman(track, vertex);
        t.kalman_ipy = ipyKalman(track, vertex);
        t.kalman_docaz = kalmanDOCAz(track, vertex);
      }
      locIPChi2 = ipChi2Velo(velo_state, vertex);
      if (locIPChi2 < t.velo_ip_chi2) {
        t.velo_ip = ipVelo(velo_state, vertex);
        t.velo_ip_chi2 = locIPChi2;
        t.velo_ipx = ipxVelo(velo_state, vertex);
        t.velo_ipy = ipyVelo(velo_state, vertex);
        t.velo_docaz = veloDOCAz(velo_state, vertex);
      }
    }

    // Get kalman filter information.
    // t.kalman_ip_chi2 = (float) track.ipChi2;
    t.z = (float) track.z;
    t.x = (float) track.state[0];
    t.y = (float) track.state[1];
    t.tx = (float) track.state[2];
    t.ty = (float) track.state[3];
    t.qop = (float) track.state[4];
    t.chi2 = (float) track.chi2;
    t.chi2V = (float) track.chi2V;
    t.chi2T = (float) track.chi2T;
    t.ndof = track.ndof;
    t.ndofV = track.ndofV;
    t.ndofT = track.ndofT;
    t.first_qop = (float) track.first_qop;
    t.best_qop = (float) track.best_qop;
    t.p = (float) track.p();
    t.pt = (float) track.pt();
    t.nhitsV = (float) track.nhitsV;
    t.nhitsT = (float) track.nhitsT;
    t.nhitsUT = (float) track.nhitsUT;
    t.chi2UT = (float) track.chi2UT;
    kalman_checker_tracks[i_track] = t;
  }
}

// Prepare downstream Kalman tracks for checker
// Note: For downstream tracks, "velo_ip" fields use the FinalFit initial state at UT plane (z=2484.6mm)
// This represents the polynomial fit state before Kalman correction
__device__ inline void prepare_downstream_kalman_tracks(
  const unsigned number_of_tracks,
  const unsigned number_of_vertices,
  const PV::Vertex* rec_vertices,
  const Allen::Views::Physics::KalmanStates downstream_kf_states,
  const ParKalmanFilter::FittedTrack* kf_tracks,
  Checker::Track* downstream_kalman_checker_tracks)
{
  for (unsigned i_track = threadIdx.x; i_track < number_of_tracks; i_track += blockDim.x) {
    ParKalmanFilter::FittedTrack track = kf_tracks[i_track];
    auto t = downstream_kalman_checker_tracks[i_track];

    // Get the UT-plane initial state (from FinalFit polynomial fit)
    const auto ut_state = downstream_kf_states.state(i_track);

    // Calculate IP using Kalman fit
    t.kalman_ip_chi2 = 9999.;
    for (unsigned i_vertex = 0; i_vertex < number_of_vertices; ++i_vertex) {
      const auto vertex = rec_vertices[i_vertex];

      float locIPChi2 = ipChi2Kalman(track, vertex);
      if (locIPChi2 < t.kalman_ip_chi2) {
        t.kalman_ip = ipKalman(track, vertex);
        t.kalman_ip_chi2 = locIPChi2;
        t.kalman_ipx = ipxKalman(track, vertex);
        t.kalman_ipy = ipyKalman(track, vertex);
        t.kalman_docaz = kalmanDOCAz(track, vertex);
      }
    }

    // Calculate "VELO" IP using UT initial state (for comparison with Kalman fit)
    // Semantic meaning: initial state IP from FinalFit at UT plane
    t.velo_ip_chi2 = 9999.;
    for (unsigned i_vertex = 0; i_vertex < number_of_vertices; ++i_vertex) {
      const auto vertex = rec_vertices[i_vertex];

      float locIPChi2 = ipChi2Velo(ut_state, vertex);
      if (locIPChi2 < t.velo_ip_chi2) {
        t.velo_ip = ipVelo(ut_state, vertex);
        t.velo_ip_chi2 = locIPChi2;
        t.velo_ipx = ipxVelo(ut_state, vertex);
        t.velo_ipy = ipyVelo(ut_state, vertex);
        t.velo_docaz = veloDOCAz(ut_state, vertex);
      }
    }

    // Get Kalman filter information from FittedTrack
    t.z = (float) track.z;
    t.x = (float) track.state[0];
    t.y = (float) track.state[1];
    t.tx = (float) track.state[2];
    t.ty = (float) track.state[3];
    t.qop = (float) track.state[4];
    t.chi2 = (float) track.chi2;
    t.chi2V = (float) track.chi2V; // Will be 0 for downstream (no VELO)
    t.chi2T = (float) track.chi2T;
    t.ndof = track.ndof;
    t.ndofV = track.ndofV; // Will be 0 for downstream (no VELO)
    t.ndofT = track.ndofT;
    t.first_qop = (float) track.first_qop;
    t.best_qop = (float) track.best_qop;
    t.p = (float) track.p();
    t.pt = (float) track.pt();
    t.nhitsV = (float) track.nhitsV; // Will be 0 for downstream (no VELO)
    t.nhitsT = (float) track.nhitsT;
    t.nhitsUT = (float) track.nhitsUT;
    t.chi2UT = (float) track.chi2UT;

    downstream_kalman_checker_tracks[i_track] = t;
  }
}
