/************************************************************************\
* (c) Copyright 2022 CERN for the benefit of the LHCb Collaboration      *
*                                                                             *
* This software is distributed under the terms of the Apache License          *
* version 2 (Apache-2.0), copied verbatim in the file "LICENSE".              *
*                                                                             *
* In applying this licence, CERN does not waive the privileges and immunities *
* granted to it by virtue of its status as an Intergovernmental Organization  *
* or submit itself to any jurisdiction.                                       *
\************************************************************************/
#include "CheckLocalizedBeamlineIP.cuh"

INSTANTIATE_ALGORITHM(check_localized_beamline_ip::check_localized_beamline_ip_t)

void check_localized_beamline_ip::check_localized_beamline_ip_t::set_arguments_size(
  ArgumentReferences<Parameters> arguments,
  const RuntimeOptions&,
  const Constants&) const
{
  set_size<dev_number_of_selected_events_t>(arguments, 1);
  set_size<host_number_of_selected_events_t>(arguments, 1);
  set_size<dev_event_list_output_t>(arguments, size<dev_event_list_t>(arguments));
}

void check_localized_beamline_ip::check_localized_beamline_ip_t::operator()(
  const ArgumentReferences<Parameters>& arguments,
  const RuntimeOptions&,
  const Constants&,
  const Allen::Context& context) const
{
  Allen::memset_async<dev_number_of_selected_events_t>(arguments, 0, context);
  Allen::memset_async<host_number_of_selected_events_t>(arguments, 0, context);
  Allen::memset_async<dev_event_list_output_t>(arguments, 0, context);

  global_function(check_localized_beamline_ip)(dim3(size<dev_event_list_t>(arguments)), m_block_dim, context)(
    arguments, m_min_state_z, m_max_state_z, m_max_state_rho_sq, m_min_local_nTracks);

  Allen::copy<host_number_of_selected_events_t, dev_number_of_selected_events_t>(arguments, context);
  reduce_size<dev_event_list_output_t>(arguments, first<host_number_of_selected_events_t>(arguments));
}

__global__ void check_localized_beamline_ip::check_localized_beamline_ip(
  check_localized_beamline_ip::Parameters parameters,
  const float min_state_z,
  const float max_state_z,
  const float max_state_rho_sq,
  const float min_local_nTracks)
{
  const unsigned event_number = parameters.dev_event_list[blockIdx.x];

  const auto velo_states = parameters.dev_velo_states_view[event_number];

  __shared__ unsigned local_nTracks;
  if (threadIdx.x == 0) local_nTracks = 0;
  __syncthreads();

  for (unsigned i = threadIdx.x; i < velo_states.size(); i += blockDim.x) {
    const auto poca = velo_states.state(i);
    const float poca_rho_sq = poca.x() * poca.x() + poca.y() * poca.y();
    const bool flag = poca.z() >= min_state_z and poca.z() < max_state_z and poca_rho_sq < max_state_rho_sq;
    if (flag) atomicAdd(&local_nTracks, 1);
  }

  __syncthreads();

  if (threadIdx.x == 0 && local_nTracks >= min_local_nTracks) {
    const auto current_event = atomicAdd(parameters.dev_number_of_selected_events.data(), 1);
    parameters.dev_event_list_output[current_event] = mask_t {event_number};
  }
}
