/*****************************************************************************\
* (c) Copyright 2022 CERN for the benefit of the LHCb Collaboration           *
*                                                                             *
* This software is distributed under the terms of the Apache License          *
* version 2 (Apache-2.0), copied verbatim in the file "COPYING".              *
*                                                                             *
* In applying this licence, CERN does not waive the privileges and immunities *
* granted to it by virtue of its status as an Intergovernmental Organization  *
* or submit itself to any jurisdiction.                                       *
\*****************************************************************************/
#include "PlumeLumiCounters.cuh"
#include "LumiCommon.cuh"

INSTANTIATE_ALGORITHM(plume_lumi_counters::plume_lumi_counters_t)

void plume_lumi_counters::plume_lumi_counters_t::set_arguments_size(
  ArgumentReferences<Parameters> arguments,
  const RuntimeOptions&,
  const Constants&) const
{
  // the total size of output info is proportional to the lumi summaries
  set_size<dev_lumi_infos_t>(
    arguments, Lumi::Constants::n_plume_counters * first<host_lumi_summaries_count_t>(arguments));
}

void plume_lumi_counters::plume_lumi_counters_t::init()
{
  std::map<std::string, std::pair<unsigned, unsigned>> schema = m_lumi_counter_schema;
  std::map<std::string, std::pair<float, float>> shifts_and_scales = m_lumi_counter_shifts_and_scales;

  unsigned c_idx(0u);
  for (auto counter_name : Lumi::Constants::plume_counter_names) {
    if (schema.find(counter_name) == schema.end()) {
      std::cout << "LumiSummary schema does not use " << counter_name << std::endl;
    }
    else {
      m_offsets_and_sizes[2 * c_idx] = schema[counter_name].first;
      m_offsets_and_sizes[2 * c_idx + 1] = schema[counter_name].second;
    }
    if (shifts_and_scales.find(counter_name) == shifts_and_scales.end()) {
      m_shifts_and_scales[2 * c_idx] = 0.f;
      m_shifts_and_scales[2 * c_idx + 1] = 1.f;
    }
    else {
      m_shifts_and_scales[2 * c_idx] = shifts_and_scales[counter_name].first;
      m_shifts_and_scales[2 * c_idx + 1] = shifts_and_scales[counter_name].second;
    }
    ++c_idx;
  }
}

void plume_lumi_counters::plume_lumi_counters_t::operator()(
  const ArgumentReferences<Parameters>& arguments,
  const RuntimeOptions&,
  const Constants&,
  const Allen::Context& context) const
{
  // do nothing if no lumi event
  if (first<host_lumi_summaries_count_t>(arguments) == 0) return;

  Allen::memset_async<dev_lumi_infos_t>(arguments, 0, context);

  global_function(plume_lumi_counters)(dim3(4u), m_block_dim, context)(
    arguments, first<host_number_of_events_t>(arguments), m_offsets_and_sizes, m_shifts_and_scales);
}

__global__ void plume_lumi_counters::plume_lumi_counters(
  plume_lumi_counters::Parameters parameters,
  const unsigned number_of_events,
  const offsets_and_sizes_t offsets_and_sizes,
  const shifts_and_scales_t shifts_and_scales)
{
  for (unsigned event_number = blockIdx.x * blockDim.x + threadIdx.x; event_number < number_of_events;
       event_number += blockDim.x * gridDim.x) {
    unsigned lumi_evt_index = parameters.dev_lumi_event_indices[event_number];

    // skip non-lumi event
    if (lumi_evt_index == parameters.dev_lumi_event_indices[event_number + 1]) continue;

    // loop over lumi channels
    const Plume_* pl = parameters.dev_plume + event_number;

    float plume_counters_ADCsum = 0.f;

    std::array<int32_t, 2> plume_counters_ovt = {0u, 0u};

    std::array<float, 2 * Lumi::Constants::n_plume_lumi_channels> plume_counters = {
      0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f,
      0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f};

    std::array<float, Lumi::Constants::n_plume_channels> plume_counters_timing = {
      0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f,
      0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f, 0.f};

    for (unsigned feb = 0; feb < 2; feb++) {
      unsigned channel_offset = feb * Lumi::Constants::n_plume_lumi_channels;
      for (unsigned channel = 0; channel < Lumi::Constants::n_plume_lumi_channels; ++channel) {
        plume_counters_ADCsum += pl->ADC_counts.at(channel_offset + channel);
        plume_counters[feb * Lumi::Constants::n_plume_lumi_channels + channel] +=
          pl->ADC_counts.at(channel_offset + channel);
        // get the corresponding overthreshold bit
      }
      plume_counters_ovt[feb] = pl->ovr_th[feb] & ((1u << Lumi::Constants::n_plume_lumi_channels) - 1);
    }
    // get average
    plume_counters_ADCsum = plume_counters_ADCsum / Lumi::Constants::n_plume_lumi_channels / 2.f;

    // timing counters
    const auto shift_two_febs = 44;
    for (unsigned int i = 0; i < Lumi::Constants::n_plume_channels; ++i) {
      plume_counters_timing[i] =
        pl->ADC_counts.at(shift_two_febs + i * 2 + 1) - pl->ADC_counts.at(shift_two_febs + i * 2);
    }

    unsigned info_offset = Lumi::Constants::n_plume_counters * lumi_evt_index;
    const unsigned idx_start_adc_counters = Lumi::Constants::n_plume_counters - Lumi::Constants::n_plume_channels -
                                            2 * Lumi::Constants::n_plume_lumi_channels;
    const unsigned idx_start_time_counters = Lumi::Constants::n_plume_counters - Lumi::Constants::n_plume_channels;

    // filling ADCsum average
    fillLumiInfo(
      parameters.dev_lumi_infos[info_offset],
      offsets_and_sizes[0],
      offsets_and_sizes[1],
      plume_counters_ADCsum,
      shifts_and_scales[0],
      shifts_and_scales[1]);

    // filling ovt bits
    for (unsigned i = 1u; i < idx_start_adc_counters; ++i) {
      fillLumiInfo(
        parameters.dev_lumi_infos[info_offset + i],
        offsets_and_sizes[2 * i],
        offsets_and_sizes[2 * i + 1],
        plume_counters_ovt[i - 1],
        shifts_and_scales[2 * i],
        shifts_and_scales[2 * i + 1]);
    }
    // filling the ADCs for each PMT
    for (unsigned i = idx_start_adc_counters; i < idx_start_time_counters; ++i) {
      fillLumiInfo(
        parameters.dev_lumi_infos[info_offset + i],
        offsets_and_sizes[2 * i],
        offsets_and_sizes[2 * i + 1],
        plume_counters[i - idx_start_adc_counters],
        shifts_and_scales[2 * i],
        shifts_and_scales[2 * i + 1]);
    }
    // filling the counters for TIMING PMTs
    for (unsigned i = idx_start_time_counters; i < Lumi::Constants::n_plume_counters; ++i) {
      fillLumiInfo(
        parameters.dev_lumi_infos[info_offset + i],
        offsets_and_sizes[2 * i],
        offsets_and_sizes[2 * i + 1],
        plume_counters_timing[i - idx_start_time_counters],
        shifts_and_scales[2 * i],
        shifts_and_scales[2 * i + 1]);
    }
  }
}
